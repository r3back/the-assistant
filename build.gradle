plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

defaultTasks 'build'

def okaeri_dependencies = ['eu.okaeri:okaeri-platform-bukkit:0.4.0-preview.72',
                           'eu.okaeri:okaeri-configs-json-simple:4.0.1',
                           'eu.okaeri:okaeri-persistence-redis:1.5.12',
                           'eu.okaeri:okaeri-persistence-jdbc:1.5.12',
                           'eu.okaeri:okaeri-persistence-mongo:1.5.12'] as HashSet

def driver_dependencies = ['com.zaxxer:HikariCP:4.0.3',
                           'org.mariadb.jdbc:mariadb-java-client:2.7.3',
                           'com.h2database:h2:2.1.214',
                           'mysql:mysql-connector-java:8.0.27',
                           'org.mongodb:mongodb-driver-sync:4.6.1'] as HashSet

def assistant_dependencies = [':plugin',
                              ':api',
                              ':addons:addons-commons',
                              ':nms:nms-commons'] as HashSet

group = 'com.github.r3back'
version = '3.2.10'
project.ext.okaeri_dependencies = okaeri_dependencies
project.ext.driver_dependencies = driver_dependencies
project.ext.assistant_dependencies = assistant_dependencies

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    repositories {
        mavenCentral()
        //NMS Repo
        maven {
            url 'https://repo.rosewooddev.io/repository/public/'
        }
        maven {
            url 'https://libraries.minecraft.net/'
        }
        //Spigot API Repo
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        //Paper
        maven {
            url 'https://papermc.io/repo/repository/maven-public/'
        }
        //Okaeri
        maven {
            url 'https://storehouse.okaeri.eu/repository/maven-public/'
        }
        //NBT API
        maven {
            url 'https://repo.codemc.org/repository/maven-public/'
        }
        maven {
            url 'https://jitpack.io'
        }
        //SlimeWorld
        maven {
            url 'https://repo.rapture.pw/repository/maven-snapshots/'
        }
        maven {
            url 'https://repo.rapture.pw/repository/maven-releases/'
        }
    }

    dependencies {
        implementation("org.jetbrains:annotations:22.0.0")

        compileOnly("org.projectlombok:lombok:1.18.22")

        // Enable lombok annotation processing
        annotationProcessor("org.projectlombok:lombok:1.18.22")
    }

    checkstyle {
        toolVersion = '8.20'
        configFile = file(rootProject.projectDir.getAbsolutePath() + '/config/checkstyle/checkstyle.xml')
        ignoreFailures = false
        showViolations = true
        showViolations = true
        maxWarnings = 0
        sourceSets = [sourceSets.main]
    }
}



